subprojects {
    apply plugin: 'java'

    group = 'com.dmdirc.parser'

    sourceSets {
        main {
            java {
                srcDir '../src'
            }
        }
        test {
            java {
                srcDir '../test'
            }
        }
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories.mavenCentral()

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.+'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.+'
    }

}

project("irc") {
    sourceSets {
        main {
            java {
                exclude 'com/dmdirc/parser/common/**'
                exclude 'com/dmdirc/parser/interfaces/**'
            }
        }
        test {
            java {
                exclude 'com/dmdirc/parser/common/**'
                exclude 'com/dmdirc/parser/interfaces/**'
            }
        }
    }

    dependencies {
        compile find("common")
    }
}

project("common") {
    sourceSets {
        main {
            java {
                exclude 'com/dmdirc/parser/irc/**'
            }
        }
        test {
            java {
                exclude 'com/dmdirc/harness/**'
                exclude 'com/dmdirc/parser/irc/**'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

def find(name) {
    if (allprojects.find { it.name == name }) {
        project(name)
    } else if (allprojects.find { it.name == 'plugins:' + name }) {
        project('parser:' + name)
    } else if (allprojects.find { it.name == 'modules:plugins:' + name }) {
        project('modules:parser:' + name)
    } else {
        println "Couldn't find project $name"
    }
}
